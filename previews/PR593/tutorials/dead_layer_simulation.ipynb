{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Inactive layer simulation: True coaxial detector"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Set the physical models for the inactive layer (dead layer) simulation with YAML\n",
    "- using drift model for the inactive layer\n",
    "- using constant lifetime trapping model\n",
    "- using mobility-tied diffusion model (by calling the `calculate_mobility` function with the drift model)"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using Plots\n",
    "using Unitful\n",
    "using SolidStateDetectors\n",
    "T = Float64;"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "the geometry parameters of the model for following display"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "det_rin = 1u\"mm\"\n",
    "det_z = det_r = 10u\"mm\"\n",
    "z_draw = det_z/2\n",
    "pn_r = 8.957282u\"mm\" # this one was calculated by searching the zero impurity point (displayed in the following section)\n",
    "\n",
    "sim = Simulation{T}(SSD_examples[:TrueCoaxial])\n",
    "cfn = SSD_examples[:TrueCoaxial]\n",
    "print(open(f -> read(f, String), cfn))\n",
    "plot(sim.detector, xunit = u\"mm\", yunit = u\"mm\", zunit = u\"mm\")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "> In the configuration file shown above, the position of the PN junction boundary `pn_r` was calculated as the position  where the density is zero."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Display the impurity profile defined"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "r_list = 0u\"mm\":0.01u\"mm\":det_r\n",
    "imp_list = map(r -> let pt::CylindricalPoint{T} = CylindricalPoint(r, 0u\"°\", z_draw)\n",
    "    SolidStateDetectors.get_impurity_density(sim.detector.semiconductor.impurity_density_model, pt) * 1e-6u\"cm^-3\"\n",
    "end, r_list)\n",
    "plot(r_list, imp_list, xlabel = \"r / mm\", ylabel = \"Impurity density / cm\\$^{-3}\\$\", unitformat = :nounit, label = \"\",\n",
    "    color = :darkblue, lw = 2, grid = :on, xlims = (0, 10), ylims = (-2e10, 1e11))\n",
    "vline!([pn_r], lw = 2, ls = :dash, color = :darkred, label = \"PN junction boundary\")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Dispaly the mobility curve"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using SolidStateDetectors: Electron, Hole\n",
    "cdm = sim.detector.semiconductor.charge_drift_model\n",
    "depth_list = 0u\"mm\":0.01u\"mm\":(det_r-pn_r)\n",
    "mobility_list = map(depth -> let pt::CartesianPoint{T} = CartesianPoint(det_r - depth, 0, z_draw)\n",
    "    (µe = SolidStateDetectors.calculate_mobility(cdm, pt, Hole) * 10000u\"cm^2/(V*s)\",\n",
    "     µh = SolidStateDetectors.calculate_mobility(cdm, pt, Electron) * 10000u\"cm^2/(V*s)\")\n",
    "end, depth_list)\n",
    "plot(depth_list,  getfield.(mobility_list, :µh), label = \"Hole\", lw = 4)\n",
    "plot!(depth_list, getfield.(mobility_list, :µe), label = \"Electron\", lw = 4)\n",
    "plot!(xlabel = \"Depth to surface / mm\", ylabel = \"Mobility / cm\\$^2\\$/Vs\", unitformat = :nounit, legend = :topleft, xlims = (0u\"mm\", det_r-pn_r))"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Calculate the electric field\n",
    "To simulate the inactive layer, we need to calculate the electric field on a very fine grid."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "calculate_electric_potential!(sim, max_n_iterations = 10, grid = Grid(sim), verbose = false, depletion_handling = true)\n",
    "g = sim.electric_potential.grid\n",
    "ax1, ax2, ax3 = g.axes\n",
    "bulk_tick_dis = 0.05u\"mm\"\n",
    "dl_tick_dis   = 0.01u\"mm\"\n",
    "user_additional_ticks_ax1 = sort(vcat(ax1.interval.left*u\"m\":bulk_tick_dis:pn_r, pn_r:dl_tick_dis:ax1.interval.right*u\"m\"))\n",
    "user_ax1 = typeof(ax1)(ax1.interval, SolidStateDetectors.to_internal_units.(user_additional_ticks_ax1))\n",
    "user_g = typeof(g)((user_ax1, ax2, ax3))\n",
    "calculate_electric_potential!(sim, refinement_limits = 0.1, grid = user_g, depletion_handling = true)\n",
    "calculate_electric_field!(sim)\n",
    "calculate_weighting_potential!(sim, 1, depletion_handling = true)\n",
    "calculate_weighting_potential!(sim, 2, depletion_handling = true);\n",
    "plot(\n",
    "    begin\n",
    "        imp = plot(sim.imp_scale, φ = 0, xunit = u\"mm\", yunit = u\"mm\", title = \"impurity scale\")\n",
    "        vline!([pn_r], lw = 2, ls = :dash, color = :darkred, label = \"PN junction boundary\", legendfontsize = 6)\n",
    "    end,\n",
    "    begin\n",
    "        plot(sim.point_types, φ = 0, xunit = u\"mm\", yunit = u\"mm\", title = \"point types\")\n",
    "        vline!([pn_r], lw = 2, ls = :dash, color = :darkred, label = \"PN junction boundary\", legendfontsize = 6)\n",
    "    end,\n",
    "    begin\n",
    "        plot(sim.electric_potential, xunit = u\"mm\", yunit = u\"mm\", title = \"electric potential\")\n",
    "        vline!([pn_r], lw = 2, ls = :dash, color = :darkred, label = \"PN junction boundary\", legendfontsize = 6)\n",
    "    end,\n",
    "    begin\n",
    "        plot(sim.electric_field, xunit = u\"mm\", yunit = u\"mm\", title = \"electric field\", clims = (0, 100*2000))\n",
    "        vline!([pn_r], lw = 2, ls = :dash, color = :darkred, label = \"PN junction boundary\", legendfontsize = 6)\n",
    "    end,\n",
    "    size = (800, 600), layout = (2, 2),\n",
    ")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Pulse shape and CCE simulation"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "depth_list = 0.1u\"mm\":0.1u\"mm\":(det_r-pn_r)\n",
    "totTime = 5u\"µs\"\n",
    "totEnergy = 1u\"keV\" # --> simulating ~339 carrier pairs\n",
    "N = Int(totEnergy÷2.95u\"eV\")\n",
    "\n",
    "pulse_plot = plot()\n",
    "eff_list = map(depth -> begin\n",
    "    r = det_r-depth\n",
    "    energy_depos = fill(2.95u\"eV\", N)\n",
    "    starting_positions = repeat([CartesianPoint(r, 0, z_draw)], N)\n",
    "    evt = Event(starting_positions, energy_depos)\n",
    "    simulate!(evt, sim, Δt = 1u\"ns\", max_nsteps = round(Int, totTime / 1u\"ns\"), diffusion = true, end_drift_when_no_field = false, self_repulsion = false)\n",
    "    pulse = evt.waveforms[1]\n",
    "    plot!(pulse_plot, pulse, label = \"Depth: $(round(typeof(depth), depth, digits = 1))\", lw = 2, yunit = u\"eV/V\")\n",
    "    maximum(pulse.signal)/N\n",
    "end, depth_list)\n",
    "plot!(pulse_plot, legend = :topright, xlabel = \"Time / ns\", ylabel = \"Amplitude / e\", unitformat = :nounit)\n",
    "cce_plot = plot(depth_list, eff_list, xlabel = \"Depth to surface / mm\", ylabel = \"Charge collection efficiency\", lw = 2, color = :black, label = \"\", unitformat = :nounit)\n",
    "plot(pulse_plot, cce_plot, layout = (1, 2), size = (1000, 400), margin = 5Plots.mm)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.10"
  },
  "kernelspec": {
   "name": "julia-1.10",
   "display_name": "Julia 1.10.10",
   "language": "julia"
  }
 },
 "nbformat": 4
}